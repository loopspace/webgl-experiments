<!DOCTYPE html>
<html lang="en">
  <head>
    <meta charset="utf-8">
    <title>webgl Experiments: Voronoi Diagrams</title>
    <script src="../utilities/sylvester.js"></script>
    <script src="../utilities/glUtils.js"></script>
    <script src="voronoi.js"></script>
    <script src="main.js"></script>
    <link href="https://fonts.googleapis.com/css?family=Sunflower:300"
      rel="stylesheet">
    <link href="voronoi.css" rel="stylesheet">
<script id="shader-fs" type="x-shader/x-fragment">
  //The interpolated coordinate for this fragment
  varying highp vec2 vCoordinate;

  uniform sampler2D pitch; // image of pitch for background
  uniform lowp vec2 pts[11]; // positions of players
  uniform lowp vec3 params[11]; // variations of distance
  uniform lowp float wgts;
  uniform lowp float dlys;
  uniform lowp float exts;
  uniform lowp float lin;
  uniform highp float aspect; // aspect ratio of pitch image
      
  void main()
  {
      lowp float d = 2.; // shortest actual distance
      lowp float dd; // actual distance to current point
      lowp float a = 2.; // alternative metric distance
      lowp float ad; // alternative metric distance 
      lowp float ld; // variations of distance
      lowp float p = 0.; // index of nearest player
      lowp vec2 pt = pts[0]; // location of nearest player
      lowp float s;
      lowp float t;
      lowp float w = 0.; // for locating boundary between regions
      lowp vec2 tc;

      // pitch background
      lowp vec4 pcol = texture2D(pitch, vCoordinate);
      // highlight colour
      lowp vec4 hcol = vec4(1.,0.,1.,1.);
    
      for (int i = 0; i < 11; i++) {
	  // Adjust vector to i-th point to take aspect ratio into account
	  tc = vCoordinate - pts[i];
	  tc.x *= aspect;
	  // Distance to current player
	  dd = length(tc);

	  // Adjusted distance, if requested
	  ld = -(1. - lin) * log(
	      max(.0001,
		  (1. - (1. + exts * (params[i].z - 1.)) * dd)))
	      + lin * dd;
	  
	  ad = (1. + wgts * (params[i].x - 1.)) * ld + dlys *
	      params[i].y;

	  // s is 0 if the new adjusted distance is shorter
	  s = 1. - (1. - step(a,ad));
	  // t is 0 if the new actual distance is shorter
	  t = 1. - (1. - step(d,dd));
	  // w is 1 if there is a new shortest distance and it is
	  // nearly the same as the current distance, or if the latest
	  // distance is not the shortest but is very close
	  w = (1.-s)*( step(abs(d*d - dd*dd),0.005 * distance(pts[i],pt)) )
	      + s* max(w, step(abs(d*d - dd*dd),0.005 *
			       distance(pts[i],pt)));
	  // adjust the index of the current point
          p = (1.-s)*(float (i)) + s*p;
	  // adjust the location of the current point
	  pt = (1.-s)*pts[i] + s*pt;
	  // shortest adjusted distance
          a = s*a + (1.-s)*ad;
	  // shortest actual distance
          d = t*d + (1.-t)*dd;
      }

      // highlight the player locations or boundaries
      s = max( step(d,.01) , w);
    
      gl_FragColor = hcol * s + (1. - s)* pcol;
  }
    </script>
    <script id="shader-vs" type="x-shader/x-vertex">
      attribute vec3 aVertexPosition;
      attribute vec2 aVertexCoordinate;
    
      uniform mat4 uMVMatrix;
      uniform mat4 uPMatrix;
      
      varying highp vec2 vCoordinate;
    
      void main(void) {
        gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
        vCoordinate = aVertexCoordinate;
      }
    </script>
  </head>
  <body onload="start()">
    <div class="texture">
      <img id="pitch" src="football.png">
    </div>
    <div id="container">
    <canvas id="glCanvas" width="640" height="480">
      Your browser doesn't appear to support the 
      <code>&lt;canvas&gt;</code> element.
    </canvas>
    </div>
    <div id="help" class="help">
      <h3>Voronoi Football</h3>
      <p>
	This page allows you to explore the Voronoi diagram for a
	football team.
      </p>
      <p>
      </p>
    </div>
    <div id="helpsign" class="help">
      <a href="#" id="helplink">?</a>
    </div>
  </body>
</html>
